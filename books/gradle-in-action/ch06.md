# 6 多项目构建

配套代码 <https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06>

---

[TOC]

## 6.2 组装多项目构建

### 6.2.1 `settings` 文件介绍

[清单 6.1 通过路径添加子项目的 `settings` 文件](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_01-todo-settings-file)

```gradle
// todo/settings.gradle
include 'model' // 如果是多级目录，用冒号 : 分隔，而不是反斜杠 /。
include 'repository', 'web' // 字符串数组
```

```bash
$ gradle projects
:projects

------------------------------------------------------------
Root project
------------------------------------------------------------

Root project 'todo'
+--- Project ':model' # 注意：分隔符是冒号 :
+--- Project ':repository'
\--- Project ':web'
```

DSL API

- [include](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html#org.gradle.api.initialization.Settings:include(java.lang.String[]))

> As such, the supplied paths must use the ':' character as separator (and NOT '/').

### 6.2.2 理解 `settings` API 表示

DSL API

- [Settings](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html)

Java API

- [Interface ProjectDescriptor](https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/ProjectDescriptor.html)
- [Interface Settings](https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/Settings.html)

### 6.2.3 `settings` 执行

### 6.2.4 获取 `settings` 文件

### 6.2.5 分层布局与扁平布局

DSL API

- [includeFlat](https://docs.gradle.org/current/dsl/org.gradle.api.initialization.Settings.html#org.gradle.api.initialization.Settings:includeFlat(java.lang.String[]))

## 6.3 配置子项目

### 6.3.1 理解 Project API 表示

```gradle
// todo/settings.gradle
include 'model', 'repository', 'web'
```

[清单 6.2 定义特定的项目构建逻辑](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_02-todo-specific-project-behavior)

```gradle
// todo/build.gradle
ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']

group = projectIds.group
version = projectIds.version

project(':model') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
}

project(':repository') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
}

project(':web') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'jetty'

    repositories {
        mavenCentral()
    }

    dependencies {
        providedCompile 'javax.servlet:servlet-api:2.5'
        runtime 'javax.servlet:jstl:1.1.2'
    }
}
```

为子项目 model 执行 `build` task

```bash
$ gradle :model:build
:model:compileJava
:model:processResources UP-TO-DATE
:model:classes
:model:jar
:model:assemble
:model:compileTestJava UP-TO-DATE
:model:processTestResources UP-TO-DATE
:model:testClasses UP-TO-DATE
:model:test UP-TO-DATE
:model:check UP-TO-DATE
:model:build
```

### 6.3.3 声明项目依赖

```gradle
// settings.gradle
include 'model', 'repository', 'web'
```

[清单 6.3 声明项目依赖](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_03-todo-project-dependencies)

```gradle
// todo/build.gradle
ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']

group = projectIds.group
version = projectIds.version

project(':model') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
}

project(':repository') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'

    dependencies {
        compile project(':model') // 依赖子项目 model
    }
}

project(':web') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'jetty'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':repository') // 依赖子项目 repository
        providedCompile 'javax.servlet:servlet-api:2.5' // 依赖外部项目
        runtime 'javax.servlet:jstl:1.1.2'
    }
}
```

从根项目执行 task

```bash
$ gradle build
:model:compileJava
:model:processResources UP-TO-DATE
:model:classes
:model:jar
:model:assemble
:model:compileTestJava UP-TO-DATE
:model:processTestResources UP-TO-DATE
:model:testClasses UP-TO-DATE
:model:test UP-TO-DATE
:model:check UP-TO-DATE
:model:build
:repository:compileJava
:repository:processResources UP-TO-DATE
:repository:classes
:repository:jar
:repository:assemble
:repository:compileTestJava UP-TO-DATE
:repository:processTestResources UP-TO-DATE
:repository:testClasses UP-TO-DATE
:repository:test UP-TO-DATE
:repository:check UP-TO-DATE
:repository:build
:web:compileJava
:web:processResources UP-TO-DATE
:web:classes
:web:war
:web:assemble
:web:compileTestJava UP-TO-DATE
:web:processTestResources UP-TO-DATE
:web:testClasses UP-TO-DATE
:web:test UP-TO-DATE
:web:check UP-TO-DATE
:web:build
```

### 6.3.4 多项目部分构建

接上一小节

```bash
# 只构建 repository 项目本身，其依赖项目不参与构建。
$ gradle :repository:build -a
:repository:compileJava UP-TO-DATE
:repository:processResources UP-TO-DATE
:repository:classes UP-TO-DATE
:repository:jar UP-TO-DATE
:repository:assemble UP-TO-DATE
:repository:compileTestJava UP-TO-DATE
:repository:processTestResources UP-TO-DATE
:repository:testClasses UP-TO-DATE
:repository:test UP-TO-DATE
:repository:check UP-TO-DATE
:repository:build UP-TO-DATE

# 没有 -a 选项，其依赖项目 model 将参与构建。
$ gradle :repository:build
:model:compileJava UP-TO-DATE
:model:processResources UP-TO-DATE
:model:classes UP-TO-DATE
:model:jar UP-TO-DATE
:repository:compileJava UP-TO-DATE
:repository:processResources UP-TO-DATE
:repository:classes UP-TO-DATE
:repository:jar UP-TO-DATE
:repository:assemble UP-TO-DATE
:repository:compileTestJava UP-TO-DATE
:repository:processTestResources UP-TO-DATE
:repository:testClasses UP-TO-DATE
:repository:test UP-TO-DATE
:repository:check UP-TO-DATE
:repository:build UP-TO-DATE
```

### 6.3.5 声明跨项目的 task 依赖

```gradle
// todo/settings.gradle
include 'model', 'repository', 'web'
```

[清单 6.4 跨项目的 task 定义，没有依赖](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_04-todo-cross-project-tasks)

```gradle
// todo/build.gradle
ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']

group = projectIds.group
version = projectIds.version

// 根项目的同名 task 总是首先执行
task hello << {
    println 'Hello from root project'
}

project(':model') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'

    // 子项目的同名 task 按照项目名称的字母顺序执行
    task hello << {
        println 'Hello from model project'
    }
}

project(':repository') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'

    dependencies {
        compile project(':model')
    }

    // 项目名称的字母顺序在 model 之后
    task hello << {
        println 'Hello from repository project'
    }
}

project(':web') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'jetty'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':repository')
        providedCompile 'javax.servlet:servlet-api:2.5'
        runtime 'javax.servlet:jstl:1.1.2'
    }
}
```

```bash
$ gradle hello
:hello
Hello from root project
:model:hello
Hello from model project
:repository:hello
Hello from repository project
```

控制 task 执行顺序

[清单 6.5 声明跨项目的 task 依赖](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_05-todo-cross-project-task-dependencies)

```gradle
// todo/build.gradle
ext.projectIds = ['group': 'com.manning.gia', 'version': '0.1']

group = projectIds.group
version = projectIds.version

task hello << {
    println 'Hello from root project'
}

project(':model') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'

    // 在子项目 repository 的同名 task 后执行
    task hello(dependsOn: ':repository:hello') << {
        println 'Hello from model project'
    }
}

project(':repository') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'

    dependencies {
        compile project(':model')
    }

    task hello << {
        println 'Hello from repository project'
    }
}

project(':web') {
    group = projectIds.group
    version = projectIds.version
    apply plugin: 'java'
    apply plugin: 'war'
    apply plugin: 'jetty'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':repository')
        providedCompile 'javax.servlet:servlet-api:2.5'
        runtime 'javax.servlet:jstl:1.1.2'
    }
}
```

```bash
$ gradle hello
:hello
Hello from root project
:repository:hello
Hello from repository project
:model:hello
Hello from model project
```

### 6.3.6 定义公共行为

[清单 6.6 配置公共项目行为](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_06-todo-common-project-behavior)

```gradle
// todo/build.gradle

// 闭包内的配置适用于根项目和子项目
allprojects {
    group = 'com.manning.gia'
    version = '0.1'
}

// 配置只适用于子项目
subprojects {
    apply plugin: 'java'
}

project(':repository') {
    dependencies {
        compile project(':model')
    }
}

project(':web') {
    apply plugin: 'war'
    apply plugin: 'jetty'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':repository')
        providedCompile 'javax.servlet:servlet-api:2.5'
        runtime 'javax.servlet:jstl:1.1.2'
    }
}
```

## 6.4 独立的项目文件

根项目的配置

```gradle
// todo/build.gradle
allprojects {
    group = 'com.manning.gia'
    version = '0.1'
}

subprojects {
    apply plugin: 'java'
}
```

子项目 model 的配置

```gradle
// todo/model/build.gradle
// 自己没有配置，全部配置继承于根项目。
```

子项目 repository 的配置

```gradle
// todo/repository/build.gradle
dependencies {
    compile project(':model')
}
```

子项目 web 的配置

```gradle
// todo/web/build.gradle
apply plugin: 'war'
apply plugin: 'jetty'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':repository')
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
}
```

配套代码 <https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_07-09-todo-individual-build-scripts>

## 6.5 自定义项目

[清单 6.10 在 `settings` 文件中自定义项目脚本名称](https://github.com/bmuschko/gradle-in-action-source/tree/master/chapter06/listing_06_10-todo-custom-build-script-names)

```gradle
// todo/settings.gradle
include 'todo-model', 'todo-repository', 'todo-web'

rootProject.name = 'todo'

// 遍历子项目
rootProject.children.each {
    // 设置子项目的构建脚本名称
    it.buildFileName = it.name + '.gradle' - 'todo-'
}
```

根项目的配置

```gradle
// todo/build.gradle
allprojects {
    group = 'com.manning.gia'
    version = '0.1'
}

subprojects {
    apply plugin: 'java'
}
```

子项目 todo-model 的配置

```gradle
// todo/todo-model/model.gradle
// 自己没有配置，全部配置继承于根项目。
```

子项目 todo-repository 的配置

```gradle
// todo/todo-repository/repository.gradle
dependencies {
    compile project(':model')
}
```

子项目 todo-web 的配置

```gradle
// todo/todo-web/web.gradle
apply plugin: 'war'
apply plugin: 'jetty'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':repository')
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'javax.servlet:jstl:1.1.2'
}
```