[toc]

# 4 构建脚本概要

## 4.1 构建块

每个 Gradle 构建都包含三个基本构建块：`project`、`task` 和 `property`。每个构建包含至少一个 `project`，进而又包含一个或多个 `task`。`project` 和 `task` 暴露的属性可以用来控制构建。

Gradle 构建中的两个基本概念是 `project` 和 `task`。在多项目构建中一个 `project` 可以依赖于其他的 `project`。相似的，`task` 可以形成一个依赖关系图来确保它们的执行顺序。

### 4.1.1 项目

在 Gradle 术语中，一个项目（`project`）代表一个正在构建的组件（比如，一个 JAR 文件），或一个想要完成的目标，如部署应用程序。

每个 Gradle 构建脚本至少定义一个项目。当构建进程启动后，Gradle 基于 `build.gradle` 中的配置实例化 `org.gradle.api.Project` 类，并且能够通过 `project` 变量使其隐式可用。

当访问属性和方法时，不需要使用 `project` 变量——它会假设你是指 `Project` 实例。

```gradle
// 在不显式使用 project 变量的情况下设置项目描述
setDescription("myProject")
// 在不使用 project 变量的情况下通过 Groovy 语法来访问 name 属性
println "Description of project $name: " + project.description
```

输出结果：
```bash
$ gradle
Description of project example: myProject
```

- [Project](https://docs.gradle.org/1.7/dsl/org.gradle.api.Project.html)

### 4.1.2 任务

任务动作（task action）：当任务执行时最小的工作单元

任务依赖（task dependency）：运行一个 `task` 之前需要运行另一个 `task`，尤其是当 `task` 的运行需要另一个 `task` 的输出作为输入来完成自己的行动时更是如此。

- [Task](https://docs.gradle.org/1.7/dsl/org.gradle.api.Task.html)

### 4.1.3 属性

每个 `Project` 和 `Task` 实例都提供了可以通过 `getter` 和 `setter` 方法访问的属性。

**扩展属性**

为了添加属性，你需要使用 `ext` 命名空间。

```gradle
project.ext.myProp = 'myValue' // 只在初始声明扩展属性时需要使用 ext 命名空间

println myProp
println project.myProp
println project.ext.myProp

ext {
    someOtherProp = 123
}

someOtherProp = 456
println someOtherProp
```

输出结果：
```
$ gradle
myValue
myValue
myValue
456
```

#### Gradle 属性

Gradle 属性可以通过在 `gradle.properties` 文件中声明直接添加到项目中，这个文件位于 `<USER_HOME>/.gradle` 目录或项目的根目录下。这些属性可以通过项目实例访问。

即使有多个项目，每个用户也只能有一个 Gradle 属性文件在 `<USER_HOME>/.gradle`  目录下。在这个属性文件中声明的属性对所有的项目可用。

`gradle.properties`
```
exampleProp = myValue
someOtherProp = 455
```

`build.gradle`
```gradle
println project.exampleProp

task printGradleProperty << {
    println "Second property: $someOtherProp"
}
```

输出结果：
```
$ gradle printGradleProperty
myValue
:printGradleProperty
Second property: 455
```

**声明属性的其他方式**

- 项目属性通过 `-P` 命令行选项提供
- 系统属性通过 `-D` 命令行选项提供
- 环境属性按照 `ORG_GRADLE_PROJECT_propertyName=someValue` 的模式提供

## 4.2 使用 `task`

在默认情况下，每个新创建的 `task` 都是 `org.gradle.api.DefaultTask` 类型的，标准的 `org.gradle.api.Task` 实现。`DefaultTask` 里的所有属性都是 `private` 的。这意味着它们只能通过 `public` 的 `getter` 和 `setter` 方法来访问。幸运都是，Groovy 提供了一些语法糖，可以直接通过属性名来使用属性。在底层，Groovy 会为你调用这些方法。

- [DefaultTask](https://docs.gradle.org/1.7/javadoc/org/gradle/api/DefaultTask.html)

### 4.2.1 项目版本管理

n/a

### 4.2.2 声明 `task` 动作

动作（action）就是在 `task` 中合适的地方放置构建逻辑。`Task` 接口提供了两个相关的方法来声明 `task` 动作：`doFirst(Closure)` 和 `doLast(Closure)`。当 `task` 被执行的时候，动作逻辑被定义为闭包参数被依次执行。

```gradle
version = '0.1-SNATSHOT'

task printVersion {
    doLast {
        println "Version: $version"
    }
}

// task printVersion << {
//     println "Version: $version"
// }
```

```gradle
version = '0.1-SNATSHOT'

task printVersion {
    doFirst {
        println "Version: $version"
    }
}
```

输出结果：
```bash
$ gradle printVersion
:printVersion
Version: 0.1-SNATSHOT
```

**给现有 `task` 添加动作**

在 `task` 创建后，你可以根据需要添加很多动作。在内部，每个 `task` 都保持了一个动作列表。在运行时，它们按顺序执行。

```gradle
version = '0.1-SNATSHOT'

task printVersion {
    doFirst {
        println "Before reading the project version"
    }

    doLast {
        println "Version: $version"
    }
}

printVersion.doFirst { println "First action" } // 在动作列表的开头添加新的动作
printVersion << { println "Last action" } // 在动作列表的最后添加新的动作
```

输出结果：
```bash
$ gradle printVersion
:printVersion
First action
Before reading the project version
Version: 0.1-SNATSHOT
Last action
```

### 4.2.3 访问 `DefaultTask` 属性

Gradle 提供了一个基于 SLF4J 日志库的 `logger` 实现。除了实现常规范围的的日志级别（DEBUG、ERROR、INFO、TRACE、WARN）之外，Gradle 还增加了一些额外的日志级别。

使用 `logger` 实例来打印 `QUIET` 日志级别的版本号（）
```gradle
version = '0.1-SNAPSHOT'

task printVersion << {
    logger.quiet "Version: $version"
}
```

输出结果：
```bash
$ gradle printVersion
:printVersion
Version: 0.1-SNAPSHOT
```

`description` 属性用于描述任务的作用，`group` 属性用于定义任务的逻辑分组。
```gradle
// 创建 task 的时候，为这两个属性设置值作为参数。
version = '0.1-SNAPSHOT'

task printVersion(group: 'versioning', 'description': 'Prints project version.') << {
    logger.quiet "Version: $version"
}
```
或
```gradle
// 通过调用 setter 方法来设置属性
version = '0.1-SNAPSHOT'

task printVersion {
    group = 'versioning'
    description = 'Prints project version.'

    doLast {
        logger.quiet "Version: $version"
    }
}
```

输出结果：
```bash
$ gradle tasks
:tasks

Versioning tasks
----------------
printVersion - Prints project version.
```

### 4.2.4 定义 `task` 依赖

```gradle
version = '0.1-SNAPSHOT'

task first << { println "first" }
task second << { println "second" }

task printVersion(dependsOn: [second, first]) << {
    logger.quiet "Version: $version"
}

task third << { println "third" }
third.dependsOn('printVersion')
```

输出结果：
```bash
$ gradle third
:first
first
:second
second
:printVersion
Version: 0.1-SNAPSHOT
:third
third
```

**注意：Gradle 并不能保证 `task` 依赖的执行顺序。**

### 4.2.5 终结器 `task`

执行完 `first` 会自动触发 `second` 【注：执行完一个任务后执行另一个，与任务依赖相反。】
```gradle
task first << { println "first" }
task second << { println "second" }
first.finalizedBy second
```

输出结果：
```bash
$ gradle -q first
first
second
```

- [finalizedBy](https://docs.gradle.org/1.7/dsl/org.gradle.api.Task.html#org.gradle.api.Task:finalizedBy)

### 4.2.6 添加任意代码

**清单 4.2 通过 POGO 来表示项目版本**
```gradle
version = new ProjectVersion(0, 1)

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}

task printVersion << {
    logger.quiet "Version: $version"
}
```

输出结果：
```bash
$ gradle printVersion
:printVersion
Version: 0.1-SNAPSHOT
```

### 4.2.7 理解 `task` 配置

#### 添加 `task` 配置块

```
# File: version.properties
major=0
minor=1
release=false
```

**清单 4.3 编写一个 `task` 配置**
```gradle
ext.versionFile = file('version.properties')

// 添加 task 配置块（没有 doFirst 和 doLast 动作）
// task 配置块永远在 task 动作执行之前被执行
task loadVersion {
    // 定义 task 配置
    project.version = readVersion() 
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task printVersion << {
    logger.quiet "Version: $version"
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果：
```bash
$ gradle printVersion
Reading the version file.
:printVersion
Version: 0.1-SNAPSHOT
```

- [file](https://docs.gradle.org/1.7/dsl/org.gradle.api.Project.html#org.gradle.api.Project:file(java.lang.Object))
- [withInputStream](http://docs.groovy-lang.org/latest/html/groovy-jdk/java/io/File.html#withInputStream(groovy.lang.Closure))

**Gradle 构建生命周期阶段**

- 初始化阶段

在初始化阶段，Gradle 为项目创建了一个 `Project` 实例。

- 配置阶段

**配置代码**
```gradle
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion() 
}
```

**注意：项目每一次构建的任何配置代码都可以被执行——即使你只执行 `gradle tasks`。**

- 执行阶段

**动作代码**
```gradle
task printVersion << {
    logger.quiet "Version: $version"
}
```

### 4.2.8 声明 `task` 的 `inputs` 和 `outputs`

```
# version.properties
major=0
minor=1
release=false
```

**清单 4.4  将项目版本切换为产品发布版本**
```gradle
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion() 
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task printVersion << {
    logger.quiet "Version: $version"
}

task makeReleaseVersion(group: 'versioning', description: 'Makes project a release version.') << {
    version.release = true
    // Ant task 的 propertyfile 提供了一种便利的方式来修改属性文件
    ant.propertyfile(file: versionFile) {
        entry(key: 'release', type: 'string', operation: '=', value: 'true')
    }
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果（一直重复执行任务）：

第一次执行任务
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Fri, 06 Apr 2018 15:51:01 +0800
major=0
minor=1
release=true
```

第二次执行任务
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Fri, 06 Apr 2018 15:51:31 +0800
major=0
minor=1
release=true
```

第三次执行任务
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Fri, 06 Apr 2018 15:52:00 +0800
major=0
minor=1
release=true
```

Gradle 通过比较两个构建 `task` 的 `inputs` 和 `outputs` 来决定 `task` 是否是最新的。如果 `inputs` 和 `outputs` 没有发生变化，则认为 `task` 是最新的。因此，只有当 `inputs` 和 `outputs` 不同时，`task` 才运行；否则将跳过。

```
# version.properties
major=0
minor=1
release=false
```

**清单 4.5 通过 inputs/outputs 来添加增量式构建支持**
```gradle
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion() 
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task printVersion << {
    logger.quiet "Version: $version"
}

task makeReleaseVersion(group: 'versioning', description: 'Makes project a release version.') {
    // 在配置阶段声明 inputs/outputs
    inputs.property('release', version.release)
    outputs.file versionFile

    doLast {
        version.release = true
        ant.propertyfile(file: versionFile) {
            entry(key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果（不会重复执行任务）：

第一次执行
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Sun, 15 Oct 2017 11:26:50 +0800
major=0
minor=1
release=true
```

第二次执行
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Sun, 15 Oct 2017 11:29:24 +0800
major=0
minor=1
release=true
```

第三次执行
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion UP-TO-DATE # 属性 release 第一次从文件中读取的是初始值 false， 第二次是修改后的 true，第三次仍然是修改后的 true，所以第三次才会跳过执行。

$ cat version.properties
#Sun, 15 Oct 2017 11:29:24 +0800
major=0
minor=1
release=true
```

如果属性 release 在文件内就是 true，则第二次开始就不会重复执行。

```
# version.properties
major=0
minor=1
release=false
```

输出结果：

第一次执行任务
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties
#Fri, 06 Apr 2018 15:54:37 +0800
major=0
minor=1
release=true
```

第二次执行任务
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion UP-TO-DATE

$ cat version.properties
#Fri, 06 Apr 2018 15:54:37 +0800
major=0
minor=1
release=true
```

- [Interface TaskInputs](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/TaskInputs.html)
- [Interface TaskOutputs](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/TaskOutputs.html)
- [property](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/TaskInputs.html#property(java.lang.String,%20java.lang.Object))
- [file](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/TaskOutputs.html#file(java.lang.Object))

**延伸阅读：**

http://www.cnblogs.com/davenkin/p/gradle-learning-4.html

### 4.2.9 编写和使用自定义 `task`

当创建一个自定义 `task` 时，你需要做的是，创建一个继承 `DefaultTask` 的类。

```
# version.properties
major=0
minor=1
release=false
```

```gradle
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion() 
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task makeReleaseVersion(type: ReleaseVersionTask) {
    release = version.release
    destFile = versionFile
}

class ReleaseVersionTask extends DefaultTask {
    // 通过注解声明 task 的输入/输出
    @Input Boolean release
    @OutputFile File destFile

    ReleaseVersionTask() {
        // 在构造器中设置 task 的 group 和 description
        group = 'versioning'
        description = 'Makes project a release version.'
    }

    // 使用注解声明将被执行的方法
    @TaskAction
    void start() {
        project.version.release = true
        ant.propertyfile(file: destFile) {
            entry(key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果：
```bash
$ gradle makeReleaseVersion
Reading the version file.
:makeReleaseVersion

$ cat version.properties 
#Sun, 15 Oct 2017 16:23:03 +0800
major=0
minor=1
release=true
```

- [Annotation Type Input](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/Input.html)
- [Annotation Type OutputFile](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/OutputFile.html)
- [Annotation Type TaskAction](https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/TaskAction.html)

### 4.2.10 Gradle 的内置 `task` 类型

Gradle 的内置类型都是 `DefaultTask` 的派生类。

通过使用一个 `task` 的输出作为另一个 `task` 的输入，Gradle 就可以推断出依赖关系了。

增量式构建支持内置的 `task` 类型。在不改变任何源文件的情况下，连续多次运行的 `task` 会被标记为最新的。

```
# version.properties
major=0
minor=1
release=false
```

**清单 4.10 使用 `task` 类型来备份 ZIP 发布包**
```gradle
apply plugin: 'war'

ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion() 
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task makeReleaseVersion(group: 'versioning', description: 'Makes project a release version.') << {
    version.release = true
    ant.propertyfile(file: versionFile) {
        entry(key: 'release', type: 'string', operation: '=', value: 'true')
    }
}

task createDistribution(type: Zip, dependsOn: makeReleaseVersion) {
    // 隐式引用 War task 的输出
    from war.outputs.files

    // 把所有源文件都放到 ZIP 文件的 src 目录下
    from(sourceSets*.allSource) {
        into 'src'
    }

    // 从项目根目录下拷贝符合条件的文件
    from(rootDir) {
        // include 用于指定匹配条件
        include versionFile.name
    }
}

task backupReleaseDistribution(type: Copy) {
    // 隐式引用 createDistribution 的输出
    from createDistribution.outputs.files
    into "$buildDir/backup"
}

task release(dependsOn: backupReleaseDistribution) << {
    logger.quiet 'Releasing the project...'
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果：
```bash
$ gradle release
Reading the version file.
:makeReleaseVersion
:compileJava UP-TO-DATE
:processResources UP-TO-DATE
:classes UP-TO-DATE
:war
:createDistribution
:backupReleaseDistribution
:release
Releasing the project...

$ tree
.
|____build
| |____backup
| | |____example-0.1.zip
| |____distributions
| | |____example-0.1.zip
| |____libs
| | |____example-0.1.war
| |____tmp
| | |____war
| | | |____MANIFEST.MF
|____build.gradle
|____version.properties
```

- [Class AbstractCopyTask](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/AbstractCopyTask.html)
- [Zip](https://docs.gradle.org/1.7/dsl/org.gradle.api.tasks.bundling.Zip.html)
- [Copy](https://docs.gradle.org/1.7/dsl/org.gradle.api.tasks.Copy.html)
- [SourceSet](https://docs.gradle.org/1.7/dsl/org.gradle.api.tasks.SourceSet.html)
- [rootDir](https://docs.gradle.org/1.7/dsl/org.gradle.api.Project.html#org.gradle.api.Project:rootDir)
- [into](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/AbstractCopyTask.html#into(java.lang.Object))
- [include](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/AbstractCopyTask.html#include(java.lang.String...))
- [sourceSets { }](https://docs.gradle.org/1.7/dsl/org.gradle.api.Project.html#org.gradle.api.Project:sourceSets(groovy.lang.Closure))

### 4.2.11 `task` 规则

**`task` 规则命名模式**

该模式由两部分组成：`task` 名称的静态部分和一个占位符。它们联合起来就组成了一个动态的 `task` 名称。

**声明 `task` 规则**

简单的 `task` 可以直接通过调用 `project` 实例的方法来添加，`task` 规则只能通过 `task` 容器来添加，所以你首先需要通过调用 `getTasks()` 方法来获取对它的引用。

```
# version.properties
major=0
minor=1
release=false
```

**清单 4.12 合并相似逻辑到 task 规则中**
```gradle
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exist: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

// 添加 task 匹配规则，任务名符合规则的将会被加工处理，如 incrementMajorVersion 和 incrementMinorVersion。
tasks.addRule("Pattern: increment<Classifier>Version – Increments the project version classifier.") { String taskName -> // 闭包就是对符合规则的任务加工处理的代码
    if (taskName.startsWith('increment') && taskName.endsWith('Version')) {
        // 给符合命名模式的 task，如 incrementMajorVersion，动态添加一个 doLast 动作
        task(taskName) << {
            // 把任务名掐头掐尾，截取 major 或 minor 出来。
            String classifier = (taskName - 'increment' - 'Version').toLowerCase()
            String currentVersion = version.toString()

            switch (classifier) {
                case 'major':
                    ++version.major
                    break
                case 'minor':
                    ++version.minor
                    break
                default:
                    throw new GradleException("Invalid version type '$classifier. Allowed types: ['Major', 'Minor']")
            }

            String newVersion = version.toString()
            logger.info "Incrementing $classifier project version: $currentVersion -> $newVersion"

            ant.propertyfile(file: versionFile) {
                entry(key: classifier, type: 'int', operation: '+', value: 1)
            }
        }
    }
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果：
```bash
$ gradle tasks
Rules
-----
Pattern: increment<Classifier>Version – Increments the project version classifier.

$ gradle incrementMajorVersion
Reading the version file.
:incrementMajorVersion

$ cat version.properties 
#Sat, 07 Apr 2018 16:49:42 +0800
major=1
minor=1
release=false

$ gradle incrementMinorVersion
Reading the version file.
:incrementMinorVersion

$ cat version.properties 
#Sat, 07 Apr 2018 16:50:10 +0800
major=1
minor=2
release=false
```

- [tasks](https://docs.gradle.org/1.7/dsl/org.gradle.api.Project.html#org.gradle.api.Project:tasks)
- [addRule](https://docs.gradle.org/1.7/javadoc/org/gradle/api/NamedDomainObjectCollection.html#addRule(java.lang.String,%20groovy.lang.Closure))
- [TaskContainer](https://docs.gradle.org/1.7/javadoc/org/gradle/api/tasks/TaskContainer.html)

### 4.2.12 在 `buildSrc` 目录下构建代码

Gradle 在 `buildSrc` 目录下使源文件结构标准化。Java 代码需要放在 `src/main/java` 目录下，Groovy 代码则放在 `src/main/groovy` 目录下。位于这些目录下的代码都会被自动编译，并且被加入到 Gradle 构建脚本的 `classpath` 中。【注：和构建相关的源代码都放到 `buildSrc` 下】

```
.
|____build.gradle
|____buildSrc
| |____src
| | |____main
| | | |____groovy
| | | | |____com
| | | | | |____manning
| | | | | | |____gia
| | | | | | | |____ProjectVersion.groovy
| | | | | | | |____ReleaseVersionTask.groovy
|____version.properties
```

`ReleaseVersionTask.groovy`
```groovy
package com.manning.gia

// 需要从 Gradle API 导入类
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.OutputFile
import org.gradle.api.tasks.TaskAction

class ReleaseVersionTask extends DefaultTask {
    @Input Boolean release
    @OutputFile File destFile

    ReleaseVersionTask() {
        group = 'versioning'
        description = 'Makes project a release version.'
    }

    @TaskAction
    void start() {
        project.version.release = true
        ant.propertyfile(file: destFile) {
            entry(key: 'release', type: 'string', operation: '=', value: 'true')
        }
    }
}
```

`ProjectVersion.groovy`
```groovy
package com.manning.gia

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    @Override
    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

```gradle
// 构建脚本需要从 buildSrc 导入编译过的类
import com.manning.gia.ProjectVersion
import com.manning.gia.ReleaseVersionTask

ext.versionFile = file('version.properties')
project.version = readVersion()

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exit: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task makeReleaseVersion(type: ReleaseVersionTask) {
    release = version.release
    destFile = versionFile
}
```

输出结果：
```bash
$ gradle makeReleaseVersion
:buildSrc:clean UP-TO-DATE
:buildSrc:compileJava UP-TO-DATE
:buildSrc:compileGroovy
:buildSrc:processResources UP-TO-DATE
:buildSrc:classes
:buildSrc:jar
:buildSrc:assemble
:buildSrc:compileTestJava UP-TO-DATE
:buildSrc:compileTestGroovy UP-TO-DATE
:buildSrc:processTestResources UP-TO-DATE
:buildSrc:testClasses UP-TO-DATE
:buildSrc:test UP-TO-DATE
:buildSrc:check UP-TO-DATE
:buildSrc:build
Reading the version file.
:makeReleaseVersion
```

## 4.3 挂接到构建生命周期

构建的生命周期和钩子：初始化阶段 -> ++Hook（项目执行前 `gradle.beforeProject`）++->  配置阶段 -> ++Hook（task 图生成 `gradle.taskGraph.whenReady`）++-> 执行阶段 -> ++Hook（构建完成 `gradle.buildFinished`）++。

**Task 执行图的内部展示**

在配置时，Gradle 决定了在执行阶段要运行的 `task` 的顺序。【注：也就是任务之间的依赖关系】

### 4.3.1 挂接到 task 执行图

```gradle
apply plugin: 'war'
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exit: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

// 注册的生命周期钩子（回调闭包）在 task 图生成后被调用
gradle.taskGraph.whenReady { TaskExecutionGraph taskGraph ->
    // 查看执行图中是否包含名称为 release 的 task
    if (taskGraph.hasTask(release)) {
        if (!version.release) {
            logger.quiet "-- Hello World --"
            version.release = true
            ant.propertyfile(file: versionFile) {
                entry(key: 'release', type: 'string', operation: '=', value: 'true')
            }
        }
    }
}

task createDistribution(type: Zip) {
    from war.outputs.files

    from(sourceSets*.allSource) {
        into 'src'
    }

    from(rootDir) {
        include versionFile.name
    }
}

task backupReleaseDistribution(type: Copy) {
    from createDistribution.outputs.files
    into "$buildDir/backup"
}

task release(dependsOn: backupReleaseDistribution) << {
    logger.quiet 'Releasing the project...'
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    @Override
    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果:
```bash
$ gradle release
Reading the version file. # 配置阶段
-- Hello World -- # task 图生成
:compileJava UP-TO-DATE
:processResources UP-TO-DATE
:classes UP-TO-DATE
:war
:createDistribution
:backupReleaseDistribution
:release
Releasing the project...
```

- [TaskExecutionGraph](https://docs.gradle.org/1.7/javadoc/org/gradle/api/execution/TaskExecutionGraph.html)

### 4.3.2 实现 `task` 执行图监听器

**记住：如果将监听器添加到构建脚本中，那么你就不能直接访问 `Project` 实例了。**

```gradle
apply plugin: 'war'
ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exit: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

class ReleaseVersionListener implements TaskExecutionGraphListener {
    final static String releaseTaskPath = ':release'

    @Override
    void graphPopulated(TaskExecutionGraph taskGraph) {
        if (taskGraph.hasTask(releaseTaskPath)) {
            List<Task> allTasks = taskGraph.allTasks
            Task releaseTask = allTasks.find { it.path == releaseTaskPath }
            // 获取 task 所属的 project
            Project project = releaseTask.project

            if (!project.version.release) {
                project.logger.quiet '-- Hello Gradle --'
                project.version.release = true
                project.ant.propertyfile(file: project.versionFile) {
                    entry(key: 'release', type: 'string', operation: '=', value: 'true')
                }
            }
        }
    }
}

// 注册监听器到 task 执行图
gradle.taskGraph.addTaskExecutionGraphListener(new ReleaseVersionListener())

task createDistribution(type: Zip) {
    from war.outputs.files

    from(sourceSets*.allSource) {
        into 'src'
    }

    from(rootDir) {
        include versionFile.name
    }
}

task backupReleaseDistribution(type: Copy) {
    from createDistribution.outputs.files
    into "$buildDir/backup"
}

task release(dependsOn: backupReleaseDistribution) << {
    logger.quiet 'Releasing the project...'
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release

    ProjectVersion(Integer major, Integer minor) {
        this.major = major
        this.minor = minor
        this.release = Boolean.FALSE
    }

    ProjectVersion(Integer major, Integer minor, Boolean release) {
        this(major, minor)
        this.release = release
    }

    @Override
    String toString() {
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}
```

输出结果：
```bash
$ gradle release
Reading the version file. # 配置阶段
-- Hello Gradle -- # task 图生成
:compileJava UP-TO-DATE
:processResources UP-TO-DATE
:classes UP-TO-DATE
:war UP-TO-DATE
:createDistribution
:backupReleaseDistribution
:release
Releasing the project...
```

- [Interface TaskExecutionGraphListener](https://docs.gradle.org/1.7/javadoc/org/gradle/api/execution/TaskExecutionGraphListener.html)

### 4.3.3 初始化构建环境

跳过